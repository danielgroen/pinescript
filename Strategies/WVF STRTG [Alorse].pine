//@version=5

// https://github.com/Alorse/pinescript-strategies/blob/master/strategies/Williams%20Vix%20Fix.pine
// Tested on CARDANO / usdt | 30m
strategy('Williams Vix Fix Strategy [Alorse]', shorttitle='WVF STRTG [Alorse]', overlay=false, pyramiding=0, currency=currency.USD, default_qty_type=strategy.percent_of_equity, initial_capital=1000, default_qty_value=10, commission_type=strategy.commission.percent, commission_value=0.01)

// Bollinger Bands
bbGroup = 'Bollindger Bands'
src = input.source(title='Source', defval=close, group=bbGroup)
bbl = input.int(20, title='Length', group=bbGroup)
mult = input.float(2.0, minval=1, maxval=5, title='Standard Deviation', group=bbGroup)

basis = ta.sma(src, bbl)
dev = mult * ta.stdev(src, bbl)
upper = basis + dev
lower = basis - dev

// Williams Vix Fix
wvfGroup = 'Williams Vix Fix'
pd = input.int(20, title='LookBack Period StdDev', tooltip='LookBack Period Standard Deviation', group=wvfGroup)
lb = input.int(50, title='LookBack Period Percentile', group=wvfGroup)
ph = input.int(85, title='Highest Percentile', group=wvfGroup) * 0.01
pl = input.int(99, title='Lowest Percentile', group=wvfGroup) * 0.01

wvf = (ta.highest(close, pd) - low) / ta.highest(close, pd) * 100
sDev = mult * ta.stdev(wvf, bbl)
midLine = ta.sma(wvf, bbl)
upperBand = midLine + sDev
rangeHigh = ta.highest(wvf, lb) * ph
colNormal = wvf >= upperBand or wvf >= rangeHigh ? color.lime : color.new(color.silver, 20)


wvfInv = (high - ta.lowest(close, pd)) / ta.lowest(close, pd) * 100
sDevInv = mult * ta.stdev(wvfInv, bbl)
midLineInv = ta.sma(wvfInv, bbl)
upperBandInv = midLineInv + sDevInv
rangeHighInv = ta.lowest(wvfInv, lb) * pl
colInv = wvfInv <= upperBandInv or wvfInv <= rangeHighInv ? color.new(color.silver, 20) : color.orange

sellCond = (wvfInv <= upperBandInv or wvfInv <= rangeHighInv) and (wvfInv[1] <= upperBandInv[1] or wvfInv[1] <= rangeHighInv[1]) and close < upper ? false : true

buy = (wvf >= upperBand or wvf >= rangeHigh) and (wvf[1] >= upperBand[1] or wvf[1] >= rangeHigh[1]) and close < lower
sell = sellCond and close > upper

plot(wvf, title='Williams Vix Fix', style=plot.style_columns, color=colNormal)
plot(upperBand, title='Normal StdDev line', style=plot.style_line, linewidth=1, color=color.new(color.blue, 0))

plot(0, title='Cero line', color=color.new(color.white, 0), style=plot.style_line)

plot(wvfInv * -1, title='Williams Vix Fix Inverted', style=plot.style_columns, color=colInv)
plot(upperBandInv * -1, title='Inverted StdDev line', style=plot.style_line, linewidth=1, color=color.new(color.blue, 0))

// LIVEMODE:: ENABLE / DISABLE
enableLiveTest = input.bool(false, title="Enable live mode and stop backtesting", group="Livemode / Backtesting")
curDate = timestamp('2023, 08, 06')
targetDate = input.time(curDate, title="Start live mode from:", group="Livemode / Backtesting")
// LIVEMODE:: END

if enableLiveTest == false or time > targetDate
    strategy.entry('Long', strategy.long, when=buy)
    strategy.close('Long', when=sell)