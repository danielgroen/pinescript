//
//@author Alorse
//@version=5

// tested with cardano | 3M
strategy('StochRSI + Supertrend Strategy [Alorse]', shorttitle='StochRSI+ST STRTG [Alorse]', overlay=true, pyramiding=0, initial_capital=1000, currency=currency.USD, default_qty_type=strategy.percent_of_equity, default_qty_value=10, commission_type=strategy.commission.percent, commission_value=0.01)
smoothK = input.int(3, 'K', minval=1)
smoothD = input.int(3, 'D', minval=1)
lengthRSI = input.int(14, 'RSI Length', minval=1)
lengthStoch = input.int(14, 'Stochastic Length', minval=1)
src = input(close, title='RSI Source')
rsi1 = ta.rsi(src, lengthRSI)
k = ta.sma(ta.stoch(rsi1, rsi1, rsi1, lengthStoch), smoothK)
d = ta.sma(k, smoothD)

// MA
ma_type = input.string(title='Type', defval='EMA', options=['EMA', 'SMA'], group='Tendence Moving Average')
lenTend = input.int(200, minval=1, title='Periods', group='Tendence Moving Average')
maTend = ma_type == 'EMA' ? ta.ema(src, lenTend) : ta.sma(src, lenTend)
plot(maTend, color=color.new(color.white, 0), title='Tendence MA', linewidth=2)

// SUPERTREND
atrPeriod2 = input.int(11, 'ATR Length', group='SUPERTREND 2')
factor2 = input.int(2, 'Factor', group='SUPERTREND 2')
[supertrend2, direction2] = ta.supertrend(factor2, atrPeriod2)
plot(direction2 < 0 ? supertrend2 : na, 'Up Trend 2', color=color.new(color.green, 0), style=plot.style_linebr)
plot(direction2 < 0 ? na : supertrend2, 'Down Trend 2', color=color.new(color.red, 0), style=plot.style_linebr)

// STRATEGY
showShort = input.bool(false, title='Show Short entries', group='Strategy')

buy = close > maTend and k < 20 and ta.crossover(k, d) and direction2 < 0
sell = close < maTend and k > 80 and ta.crossunder(k, d) and direction2 > 0
closeBuy = k > 80 and ta.crossunder(k, d)
closeSell = k < 20 and ta.crossover(k, d)

// LIVEMODE:: ENABLE / DISABLE
enableLiveTest = input.bool(false, title="Enable live mode and stop backtesting", group="Livemode / Backtesting")
curDate = timestamp('2023, 08, 06')
targetDate = input.time(curDate, title="Start live mode from:", group="Livemode / Backtesting")
// LIVEMODE:: END

if enableLiveTest == false or time > targetDate
    // LONG
    strategy.entry('Long', strategy.long, when=buy)
    strategy.close('Long', when=closeBuy)

    // SHORT
    if showShort
        strategy.entry('Short', strategy.short, when=sell)
        strategy.close('Short', when=closeSell)

