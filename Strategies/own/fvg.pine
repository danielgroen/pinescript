// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© Tradingmaestro12

//@version=5
strategy(
     "Fair value gap [tradingmaestro12]", 
     initial_capital=100,
     commission_type=strategy.commission.percent,
     commission_value=0.055,
     slippage=3,
     default_qty_type=strategy.percent_of_equity,
     default_qty_value=100,
    //  margin_long=100, 
    //  margin_short=100,
     overlay=true
     )

// ============ CONSTANTS =============

long = "Long"
short = "Short"


// ============ INPUTS ================

direction = input.string('Both', options=['Both', long, short], group='Strategy')
TakeProfitInput = input.float(5.0, "Take profit %", step=0.1)
StopLossInput = input.float(3.0, "Stop loss %", step=0.1)
// TODO:: multiple tp & sp


// ============ FUNCTIONS =============
plotOrder (direction) =>
    left = bar_index + 1
    width = 20

    line.new(left, close, left + width, close, color = color.new(#787b86, 0))

    if direction == long
        box.new(
             left,
             close * (TakeProfitInput / 100 + 1),
             left + width,
             close,
             border_color=na,
              text = "price: " + str.tostring(close),
             text_color = color.white,
             text_size = size.normal,
             text_valign = text.align_bottom,
             bgcolor = color.new(#089981, 80)
             )
        box.new(
             left,
             close,
             left + width,
             close * (1 - (StopLossInput / 100)),
             border_color=na, 
             bgcolor = color.new(#f23645, 80)
             )
    if direction == short
        box.new(
             left,
             close,
             left + width,
             close * (StopLossInput / 100 + 1),
             border_color=na, 
             bgcolor = color.new(#f23645, 80)
             )
        box.new(
             left,
             close * (1 - (TakeProfitInput / 100)),
             left + width,
             close,
             border_color=na,
             text = "price: " + str.tostring(close),
             text_color = color.white,
             text_size = size.normal,
             text_valign = text.align_bottom,
             bgcolor = color.new(#089981, 80)
             )

// ============ LOGICA =============

prevClose1 = close[1]
prevClose2 = close[2]

threshold = ta.cum((high - low) / low) / bar_index
bull_fvg = low > high[2] and close[1] > high[2] and (low - high[2]) / high[2] > threshold
bear_fvg = high < low[2] and close[1] < low[2] and (low[2] - high) / high > threshold

takeProfitLong = strategy.position_avg_price * (TakeProfitInput / 100 + 1)
stopLossLong = strategy.position_avg_price * (1 - (StopLossInput / 100))

takeProfitShort = strategy.position_avg_price * (1 - (TakeProfitInput / 100))
stopLossShort = strategy.position_avg_price * (StopLossInput / 100 + 1)


// ============ ORDERS =============

longCondition = close > prevClose1 and prevClose1 > prevClose2
shortCondition = close < prevClose1 and prevClose1 < prevClose2

if direction != short
    strategy.exit("TP/SL", long, limit=takeProfitLong, stop=stopLossLong)
    strategy.exit("TP/SL", short, limit=takeProfitShort, stop=stopLossShort)



if longCondition and bull_fvg
    if direction != short
        strategy.entry(long, strategy.long)
    else
        strategy.close_all()

    if strategy.position_size <= 0
        plotOrder(long)

else if shortCondition and bear_fvg
    if direction != long
        strategy.entry(short, strategy.short)
    else
        strategy.close_all()
    
    if strategy.position_size >= 0
        plotOrder(short)
